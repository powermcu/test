float4 main(
	float2 vTexcoord : TEXCOORD0,
	float4 coords: WPOS,
	uniform sampler2D tex,
	uniform float alphaCut,
	uniform int alphaOp,
	uniform float4 tintColor,
	uniform int texEnv,
	uniform int fog_mode,
	uniform float4 fogColor,
	uniform float4 texEnvColor,
	uniform float fog_near,
	uniform float fog_far,
	uniform float fog_density
	)
{
	float4 texColor = tex2D(tex, vTexcoord);
	
	// Texture Environment
	if (texEnv < 4){
		if (texEnv == 0){ // GL_MODULATE
			texColor = texColor * tintColor;
		}else if (texEnv == 1){ // GL_DECAL
			texColor.rgb = lerp(tintColor.rgb, texColor.rgb, texColor.a);
			texColor.a = tintColor.a;
		}else if (texEnv == 2){ // GL_BLEND
			texColor.rgb = lerp(tintColor.rgb, texEnvColor.rgb, texColor.rgb);
			texColor.a = texColor.a * tintColor.a;
		}else{ // GL_ADD
			texColor.rgb = clamp(texColor.rgb + tintColor.rgb, 0.0, 1.0);
			texColor.a = texColor.a * tintColor.a;
		}
	}
	
	// Alpha Test
	if (alphaOp < 7){
		if (alphaOp == 0){
			if (texColor.a < alphaCut){
				discard;
			}
		}else if (alphaOp == 1){
			if (texColor.a <= alphaCut){
				discard;
			}
		}else if (alphaOp == 2){
			if (texColor.a == alphaCut){
				discard;
			}
		}else if (alphaOp == 3){
			if (texColor.a != alphaCut){
				discard;
			}
		}else if (alphaOp == 4){
			if (texColor.a > alphaCut){
				discard;
			}
		}else if (alphaOp == 5){
			if (texColor.a >= alphaCut){
				discard;
			}
		}else{
			discard;
		}
	}
	
	// Fogging
	if (fog_mode < 3){
		float vFog;
		if (fog_mode == 0){ // GL_LINEAR
			vFog = (fog_far - coords.z) / (fog_far - fog_near);
		}else if (fog_mode == 1){ // GL_EXP
			vFog = exp(-fog_density * coords.z);
		}else{ // GL_EXP2
			const float LOG2 = -1.442695;
			float d = fog_density * coords.z;
			vFog = exp(d * d * LOG2);
		}
		vFog = clamp(vFog, 0.0, 1.0);
		texColor.rgb = lerp(fogColor.rgb, texColor.rgb, vFog);
	}
	
	return texColor;
}
